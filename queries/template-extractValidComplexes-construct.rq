PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>

$supplementaryPrefixes$

# Retrieve the RDF graph of the original valid complexes
# file: template-extractValidComplexes-construct.rq

CONSTRUCT {
  ?validComplex ?property ?object .

  ?stoichioIdent ?propertyStoichio ?objectStoichio .

  ?xref ?propertyXref ?objectXref .
}

WHERE {
  ?validComplex rdf:type bp3:Complex .
  FILTER NOT EXISTS {
    ?validComplex bp3:component ?invalidComplexPart .
    ?invalidComplexPart rdf:type bp3:Complex .
    ?invalidComplexPart bp3:component ?invalidComplexPartPart .
  }

  ?validComplex ?property ?object .

  OPTIONAL {
    ?validComplex bp3:componentStoichiometry ?stoichioIdent .
    ?stoichioIdent ?propertyStoichio ?objectStoichio .
  }

  OPTIONAL {
    ?validComplex bp3:xref ?xref .
    ?xref ?propertyXref ?objectXref .
  }

}
