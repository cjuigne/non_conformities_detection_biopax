PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>

PREFIX chebi: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX reactome: <http://www.reactome.org/biopax/79/48887#>
PREFIX uniprot: <http://purl.uniprot.org/uniprot/>

# Retrieve the RDF graph of the original complexes
# file: template-extractOriginalComplexes-construct.rq

CONSTRUCT {
  ?complex ?property ?object .

  ?stoichioIdent ?propertyStoichio ?objectStoichio .

  ?xref ?propertyXref ?objectXref .
}

WHERE {
  ?complex rdf:type bp3:Complex .
  FILTER NOT EXISTS {
    ?complex bp3:component ?invalidComplexPart .
    ?invalidComplexPart rdf:type bp3:Complex .
    ?invalidComplexPart bp3:component ?invalidComplexPartPart .
  }
  ?complex ?property ?object .

  OPTIONAL {
    ?complex bp3:componentStoichiometry ?stoichioIdent .
    ?stoichioIdent ?propertyStoichio ?objectStoichio .
  }

  OPTIONAL {
    ?complex bp3:xref ?xref .
    ?xref ?propertyXref ?objectXref .
  }

}
